# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

if [[ -a ~/.secretworkrc ]]
then
  source ~/.secretworkrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# my things

makeandgo() {
	mkdir -p $1
	cd $1
}

alias md=makeandgo

alias up="cd .."

unset config_files

eval "$(rbenv init -)"

export FZF_DEFAULT_COMMAND='ag -g ""'

export BASE16_THEME="base16-default-dark"
export BASE16_SHELL="$HOME/.dotfiles/base16-shell/scripts/$BASE16_THEME.sh"
export GUARD_NOTIFY=false
[[ -s $BASE16_SHELL ]] && source $BASE16_SHELL

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

