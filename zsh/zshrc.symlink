# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

if [[ -a work.secret.zsh ]]
then
  source work.secret.zsh
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# my things

makeandgo() {
	mkdir -p $1
	cd $1
}

alias md=makeandgo

alias up="cd .."

unset config_files

eval "$(rbenv init -)"

export FZF_DEFAULT_COMMAND='ag -g ""'

export BASE16_THEME="base16-ocean"
export BASE16_SHELL="$HOME/Code/test/base16-builder-php/templates/shell/"
[ -n "$PS1" ] && [ -s $BASE16_SHELL/profile_helper.sh ] && eval "$($BASE16_SHELL/profile_helper.sh)"
# export GUARD_NOTIFY=false

if [[ -z $TMUX ]]
then
  base16_default-dark
fi

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# fg Text
# bg Background
# hl Highlighted substrings
# fg+ Text (current line)
# bg+ Background (current line)
# hl+ Highlighted substrings (current line)
# info Info
# prompt Prompt
# pointer Pointer to the current line
# marker Multi-select marker
# spinner Streaming input indicator
export FZF_DEFAULT_OPTS='
  --color 16,bg+:18,fg+:7,hl+:2,hl:2,pointer:6,prompt:6,info:5
'

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"
